plugins {
    id 'java-library'
    id 'maven-publish'
    id 'de.gliderpilot.semantic-release' version '1.4.2'
    id "com.github.spotbugs" version "5.0.14"
}

group 'io.apimap.client'

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/apimap/java-rest-interface")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("MULTI_PACKAGES_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("MULTI_PACKAGES_TOKEN")
        }
    }
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

semanticRelease {
    releaseBranches {
        include 'main'
    }
    repo {
        ghToken = System.getenv('GITHUB_TOKEN')
    }
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.14.2'
    implementation 'commons-io:commons-io:2.11.0'

    api "org.apache.httpcomponents.client5:httpclient5:5.2.1"
    implementation 'com.github.spotbugs:spotbugs-annotations:4.7.3'
    implementation 'io.apimap.api:rest-interface:2.3.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.4.0'
    testImplementation 'org.mockito:mockito-core:5.3.0'

    integrationTestImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    integrationTestImplementation 'org.mockito:mockito-junit-jupiter:5.4.0'
    integrationTestImplementation 'org.mockito:mockito-core:5.3.0'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/apimap/java-rest-client")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

integrationTest {
    useJUnitPlatform()
}

test {
    useJUnitPlatform()
}